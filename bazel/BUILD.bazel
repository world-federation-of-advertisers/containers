# Bazel build environment image.

load("@debian_packages//:packages.bzl", "debfile_layer", "debian_package_layer")
load("@rules_distroless//distroless:defs.bzl", "cacerts")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push_rule", "oci_tarball")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//:constants.bzl", "DEBIAN_JAVA_17")

JAVA = DEBIAN_JAVA_17

cacerts(
    name = "ca_certs",
    package = debfile_layer("ca-certificates"),
)

DEBIAN_PACKAGES = [
    "base-files",
    "netbase",
    "ca-certificates",
    "tzdata",
    "zip",
    "unzip",
    "git",
    "openjdk-17-jdk-headless",
    "python3",
    "python-is-python3",
    "clang",
    "swig",
    "libclang-rt-14-dev",
]

pkg_tar(
    name = "debian_packages",
    deps = [debian_package_layer(package) for package in DEBIAN_PACKAGES],
)

pkg_tar(
    name = "executables",
    srcs = [
        "@bazelisk//file",
        "@kubectl//file",
        "@wfa_rules_cue//cue:cue_cli",
    ],
    mode = "0755",
    remap_paths = {
        "/bazelisk": "/usr/bin/bazelisk",
        "/cue": "/usr/bin/cue",
        "/kubectl": "/usr/bin/kubectl",
    },
    symlinks = {
        "/usr/bin/bazel": "/usr/bin/bazelisk",
        "/bin/bash": "/usr/bin/bash",
        "/bin/sh": "/usr/bin/bash",
    },
)

oci_image(
    name = "bazel_image",
    base = "@debian_bookworm_slim",
    env = JAVA.env,
    tars = [
        ":debian_packages",
        ":ca_certs",
        ":executables",
        "@google_cloud_cli//:layer",
    ],
)

genrule(
    name = "gen_repository_file",
    outs = ["repository.txt"],
    cmd = "echo $(container_registry)/$(image_repo_prefix)/bazel > $@",
    tags = ["manual"],
)

genrule(
    name = "gen_remote_tags_file",
    outs = ["remote_tags.txt"],
    cmd = "echo $(image_tag) > $@",
    tags = ["manual"],
)

oci_push_rule(
    name = "push_bazel_image",
    image = ":bazel_image",
    remote_tags = ":remote_tags.txt",
    repository_file = ":repository.txt",
    tags = ["manual"],
)

oci_tarball(
    name = "docker_load_bazel_image",
    format = "docker",
    image = ":bazel_image",
    repo_tags = ["bazel:latest"],
)
